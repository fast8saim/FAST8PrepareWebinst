// MIT License
//
// Copyright (c) 2023 FAST8.RU
//
// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in all
// copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
// SOFTWARE.

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Объект.Публикация = "Публикация";
	
	ОС = Новый СистемнаяИнформация;
	Если СтрНайти(НРег(ОС.ВерсияОС), "windows") <> 0 Тогда
		Объект.ОперационнаяСистема = "Виндовс";
		Объект.ВидВебСервера = "ИИС";
		fast8Разделитель = "\";
	Иначе
		Объект.ОперационнаяСистема = "Линукс";
		Объект.ВидВебСервера = "Апач24";
		fast8Разделитель = "/";
	КонецЕсли;
	
	СтрокаСоединения = СтрокаСоединенияИнформационнойБазы();
	Если СтрНачинаетсяС(ВРег(СтрокаСоединения), "FILE=") Тогда
		Объект.ВидБазы = "Файл";
		Объект.ИмяСервера = Сред(СтрокаСоединения, 7, СтрДлина(СтрокаСоединения) - 8);
	Иначе
		Объект.ВидБазы = "КлиентСервер";
		ПозицияРазделителя = СтрНайти(СтрокаСоединения, ";Ref=");
		Объект.ИмяСервера = Сред(СтрокаСоединения, 7, ПозицияРазделителя - 8);
		Объект.ИмяБазы = Сред(СтрокаСоединения, ПозицияРазделителя + 6, СтрДлина(СтрокаСоединения) - ПозицияРазделителя - 7);
	КонецЕсли;
	
	Объект.ПутьКФайлуШаблона = "default.vrd";
	
	СформироватьКоманду(0);
		
КонецПроцедуры // ПриОткрытии()

#КонецОбласти // ОбработчикиСобытийФормы

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОперационнаяСистемаПриИзменении(Элемент)
	
	СформироватьКоманду(0);
	
КонецПроцедуры // ОперационнаяСистемаПриИзменении()

&НаКлиенте
Процедура ПубликацияПриИзменении(Элемент)
	
	СформироватьКоманду(10);
	
КонецПроцедуры // ПубликацияПриИзменении()

&НаКлиенте
Процедура ВидВебСервераПриИзменении(Элемент)

	СформироватьКоманду(1);
	
КонецПроцедуры // ВидВебСервераПриИзменении()

&НаКлиенте
Процедура ФайлКонфигурацииВебСервераПриИзменении(Элемент)
	
	СформироватьКоманду(10);
	
КонецПроцедуры // ФайлКонфигурацииВебСервераПриИзменении()

&НаКлиенте
Процедура ВидБазыПриИзменении(Элемент)
	
	СформироватьКоманду(2);
	
КонецПроцедуры // ВидБазыПриИзменении()

&НаКлиенте
Процедура ИмяСервераПриИзменении(Элемент)
	
	СформироватьКоманду(3);
	
КонецПроцедуры // ИмяСервераПриИзменении()

&НаКлиенте
Процедура ИмяБазыПриИзменении(Элемент)
	
	СформироватьКоманду(4);
	
КонецПроцедуры // ИмяБазыПриИзменении()

&НаКлиенте
Процедура ИмяПубликацииПриИзменении(Элемент)
	
	СформироватьКоманду(5);
	
КонецПроцедуры // ИмяПубликацииПриИзменении()

&НаКлиенте
Процедура КаталогПубликацииПриИзменении(Элемент)
	
	СформироватьКоманду(10);
	
КонецПроцедуры // КаталогПубликацииПриИзменении()

&НаКлиенте
Процедура УказатьФайлШаблонаПриИзменении(Элемент)
	
	СформироватьКоманду(10);
	
КонецПроцедуры // УказатьФайлШаблонаПриИзменении()

&НаКлиенте
Процедура ПутьКФайлуШаблонаПриИзменении(Элемент)
	
	СформироватьКоманду(10);
	
КонецПроцедуры // ПутьКФайлуШаблонаПриИзменении()

&НаКлиенте
Процедура АутентификацияОСПриИзменении(Элемент)
	
	СформироватьКоманду(10);
	
КонецПроцедуры // АутентификацияОСПриИзменении()

&НаКлиенте
Процедура ФайлКонфигурацииВебСервераНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ВыбратьКаталог("ФайлКонфигурацииВебСервера", 10, "httpd.conf");
	
КонецПроцедуры // ФайлКонфигурацииВебСервераНачалоВыбора()

&НаКлиенте
Процедура ИмяСервераНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если Не fast8ЭтоКлиентСервер Тогда
		ВыбратьКаталог("ИмяСервера", 3, "");
	КонецЕсли;
	
КонецПроцедуры // ИмяСервераНачалоВыбора()

&НаКлиенте
Процедура КаталогПубликацииНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ВыбратьКаталог("КаталогПубликации", 10, "");
	
КонецПроцедуры // КаталогПубликацииНачалоВыбора()

&НаКлиенте
Процедура ПутьКФайлуШаблонаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ВыбратьКаталог("ПутьКФайлуШаблона", 10, "default.vrd");
	
КонецПроцедуры // ПутьКФайлуШаблонаНачалоВыбора()

#КонецОбласти // ОбработчикиСобытийЭлементовШапкиФормы

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыбратьКаталог(ИмяПараметра, Порядок, Суффикс)
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	Диалог.МножественныйВыбор = Ложь;
	
	ПараметрыВыбораКаталога = Новый Структура();
	ПараметрыВыбораКаталога.Вставить("Диалог", Диалог);
	ПараметрыВыбораКаталога.Вставить("ИмяПараметра", ИмяПараметра);
	ПараметрыВыбораКаталога.Вставить("Порядок", Порядок);
	ПараметрыВыбораКаталога.Вставить("Суффикс", Суффикс);
	
	Диалог.Показать(Новый ОписаниеОповещения("КаталогЗавершение", ЭтотОбъект, ПараметрыВыбораКаталога));
	
КонецПроцедуры // ВыбратьКаталог()

&НаКлиенте
Процедура КаталогЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Диалог = ДополнительныеПараметры.Диалог;
	ИмяПараметра = ДополнительныеПараметры.ИмяПараметра;
	Порядок = ДополнительныеПараметры.Порядок;
	Суффикс = ДополнительныеПараметры.Суффикс;
		
	Если (ВыбранныеФайлы <> Неопределено) Тогда
		Объект[ИмяПараметра] = Диалог.Каталог;
		Если ЗначениеЗаполнено(Суффикс) Тогда
			Объект[ИмяПараметра] = Объект[ИмяПараметра] + fast8Разделитель + Суффикс;
		КонецЕсли;
	КонецЕсли;
	
	СформироватьКоманду(Порядок);

КонецПроцедуры // КаталогЗавершение()

&НаКлиенте
Процедура ЗаполнитьФлаги()
	
	Если Объект.Публикация = "Удаление" Тогда
		fast8ЭтоУдаление = Истина;
	Иначе
		fast8ЭтоУдаление = Ложь;
	КонецЕсли;
	
	Если Объект.ВидВебСервера = "ИИС" Тогда
		fast8ЭтоИИС = Истина;
	Иначе
		fast8ЭтоИИС = Ложь;
	КонецЕсли;

	Если Объект.ОперационнаяСистема = "Виндовс" Тогда
		fast8ЭтоВиндовс = Истина;
		fast8Разделитель = "\";
	Иначе
		fast8ЭтоВиндовс = Ложь;
		fast8Разделитель = "/";
	КонецЕсли;
	
	Если Объект.ВидБазы = "КлиентСервер" Тогда
		fast8ЭтоКлиентСервер = Истина;
	Иначе
		fast8ЭтоКлиентСервер = Ложь;
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьФлаги()

&НаКлиенте
Функция КаталогПубликацииПоУмолчанию()
	
	Если fast8ЭтоИИС Тогда
		Каталог = "C:\inetpub\";
	ИначеЕсли fast8ЭтоВиндовс Тогда
		Каталог = "C:\WWWRoot\";
	Иначе
		Каталог = "/var/www/";
	КонецЕсли;
	
	Возврат Каталог;
	
КонецФункции // КаталогПубликацииПоУмолчанию()

&НаКлиенте
Функция ФайлКонфигурацииВебСервераПоУмолчанию()
	
	Если fast8ЭтоИИС Тогда
		Файл = "";
	ИначеЕсли fast8ЭтоВиндовс Тогда
		Файл = "C:\Program Files\apache24\conf\httpd.conf";
	Иначе
		Файл = "/etc/apache2/httd.conf";
	КонецЕсли;

	Возврат Файл;
	
КонецФункции // ФайлКонфигурацииВебСервераПоУмолчанию()

&НаКлиенте
Процедура ЗаполнитьИмяФайловойБазы()
	
	ПозицияРазделителя = СтрНайти(Объект.ИмяСервера, fast8Разделитель, НаправлениеПоиска.СКонца);
	Объект.ИмяБазы = Прав(Объект.ИмяСервера, СтрДлина(Объект.ИмяСервера) - ПозицияРазделителя);
	
КонецПроцедуры // ЗаполнитьИмяФайловойБазы()

&НаКлиенте
Процедура СформироватьКоманду(Порядок)
	
	ЗаполнитьФлаги();
	
	Если Не fast8ЭтоВиндовс И fast8ЭтоИИС Тогда
		Объект.ВидВебСервера = "Апач24";
		ЗаполнитьФлаги();
	КонецЕсли;
	
	Если fast8ЭтоКлиентСервер И Элементы.ИмяСервера.Заголовок = "Путь к базе" Тогда
		Элементы.ИмяСервера.Заголовок = "Имя сервера";
	ИначеЕсли Не fast8ЭтоКлиентСервер И Элементы.ИмяСервера.Заголовок = "Имя сервера" Тогда
		Элементы.ИмяСервера.Заголовок = "Путь к базе";
	КонецЕсли;
	Если fast8ЭтоКлиентСервер И Элементы.ИмяСервера.КнопкаВыбора = Истина Тогда
		Элементы.ИмяСервера.КнопкаВыбора = Ложь;
	ИначеЕсли Не fast8ЭтоКлиентСервер И Элементы.ИмяСервера.КнопкаВыбора = Ложь Тогда
		Элементы.ИмяСервера.КнопкаВыбора = Истина;
	КонецЕсли;
	
	Элементы.ВидБазы.Доступность = Не fast8ЭтоУдаление;
	Элементы.ИмяСервера.Доступность = Не fast8ЭтоУдаление;
	Элементы.КаталогПубликации.Доступность = Не fast8ЭтоУдаление;
	Элементы.ФайлКонфигурацииВебСервера.Доступность = Не fast8ЭтоУдаление И Не fast8ЭтоИИС;
	Элементы.УказатьФайлШаблона.Доступность = Не fast8ЭтоУдаление;
	Элементы.ПутьКФайлуШаблона.Доступность = Не fast8ЭтоУдаление И Объект.УказатьФайлШаблона;
	Элементы.АутентификацияОС.Доступность = fast8ЭтоИИС И Не fast8ЭтоУдаление;
	
	Если Порядок <= 1 Тогда
		Объект.ФайлКонфигурацииВебСервера = ФайлКонфигурацииВебСервераПоУмолчанию();
	КонецЕсли;
	Если Порядок <= 3 И Не fast8ЭтоКлиентСервер Тогда
		ЗаполнитьИмяФайловойБазы();
	КонецЕсли;
	Если Порядок <= 4 Тогда
		Объект.ИмяПубликации = Объект.ИмяБазы;
	КонецЕсли;
	Если Порядок <= 5 Тогда
		Объект.КаталогПубликации = КаталогПубликацииПоУмолчанию() + Объект.ИмяПубликации;
	КонецЕсли;
		
	
	//webinst [-publish] | -delete <веб-сервер>
	//    -wsdir <виртуальный каталог>
	//    -dir <физический каталог>
	//    -connstr <строка соединения>
	//    -confpath <путь к файлу httpd.conf>
	//    -descriptor <путь к файлу default.vrd>
	//    [-osauth]
		
	Фрагменты = Новый Массив;
	Фрагменты.Добавить("webinst");
	
	Если fast8ЭтоУдаление Тогда
		Фрагменты.Добавить("-delete");
	Иначе
		Фрагменты.Добавить("-publish");
	КонецЕсли;
	
	Если fast8ЭтоИИС Тогда
		Фрагменты.Добавить("-iis");
	ИначеЕсли Объект.ВидВебСервера = "Апач20" Тогда
		Фрагменты.Добавить("-apache2");	
	ИначеЕсли Объект.ВидВебСервера = "Апач22" Тогда
		Фрагменты.Добавить("-apache22");
	ИначеЕсли Объект.ВидВебСервера = "Апач24" Тогда
		Фрагменты.Добавить("-apache24");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ИмяПубликации) Тогда
		Фрагменты.Добавить(СтрШаблон("-wsdir %1", Объект.ИмяПубликации));
	КонецЕсли;
	
	Если Не fast8ЭтоУдаление Тогда
		
		Если ЗначениеЗаполнено(Объект.КаталогПубликации) Тогда
			Фрагменты.Добавить(СтрШаблон("-dir ""%1""", Объект.КаталогПубликации));
		КонецЕсли;
		
		Если ЗначениеЗаполнено(Объект.ИмяБазы) И ЗначениеЗаполнено(Объект.ИмяСервера) Тогда
			Если fast8ЭтоКлиентСервер Тогда
				Фрагменты.Добавить(СтрШаблон("-connstr ""Srvr=%1;Ref=%2;""", Объект.ИмяСервера, Объект.ИмяБазы));
			Иначе
				Фрагменты.Добавить(СтрШаблон("-connstr ""File=%1;""", Объект.ИмяСервера));
			КонецЕсли;
		КонецЕсли;
	
		Если Не fast8ЭтоИИС Тогда
			Фрагменты.Добавить(СтрШаблон("-confpath ""%1""", Объект.ФайлКонфигурацииВебСервера));
		КонецЕсли;

		Если Объект.УказатьФайлШаблона И ЗначениеЗаполнено(Объект.ПутьКФайлуШаблона) Тогда
			Фрагменты.Добавить(СтрШаблон("-descriptor ""%1""", Объект.ПутьКФайлуШаблона));
		КонецЕсли;
		
		Если fast8ЭтоИИС И Объект.АутентификацияОС Тогда
			Фрагменты.Добавить("-osauth");
		КонецЕсли;
		
	КонецЕсли;
	
	fast8Результат = СтрСоединить(Фрагменты, " ");
			
КонецПроцедуры // СформироватьКоманду()

#КонецОбласти // СлужебныеПроцедурыИФункции

